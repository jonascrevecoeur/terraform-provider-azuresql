---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "azuresql_procedure Resource - terraform-provider-azuresql"
subcategory: ""
description: |-
  Manage database user defined procedures.
---

# azuresql_procedure (Resource)

Manage database user defined procedures.

**Supported**: `SQL Database`, `Synapse serverless database` 

**Not supported**: `Synapse dedicated database`


## Example Usage

```terraform
provider "azuresql" {
}

data "azuresql_sqlserver" "server" {
  server  = "mysqlserver"
}

data "azuresql_database" "database" {
  server  = data.azuresql_sqlserver.server.id
  name    = "mydatabase"
}


data "azuresql_schema" "dbo" {
    database  = data.azuresql_database.database.id
    name      = "dbo"
}

# Define a procedure via the properties argument
resource "azuresql_procedure" "sum_props" {
  database    = data.azuresql_database.database.id
  name        = "sum"
  schema      = data.azuresql_schema.dbo.id
  properties  = {
    arguments = [
      {
        name  = "a"
        type  = "int"
      },
      {
        name  = "b"
        type  = "int"
      }
    ]
    executor      = "self"
    definition    = "select @a + @b as sum"
  }
}

# Define the same procedure via the raw argument
resource "azuresql_procedure" "sum_raw" {
    database    = data.azuresql_database.database.id
    name        = "sum2"
    schema      = data.azuresql_schema.dbo.id
    raw         = <<-EOT
        create procedure dbo.sum2 @a int, @b int
        returns int
        with execute as self
        as 
        BEGIN
          select @a + @b as sum
        END
    EOT
}
```

~> Hint: Terraform will not automatically detect all dependencies on other azuresql resources specified as text in the `raw` or `defintion` parameter. This can be resolved by adding a `replace_triggered_by` lifecycle rule to the `azuresql_procedure` resource.

<!-- schema generated by tfplugindocs -->
## Schema

-> `azuresql_procedure` offers two constructors. You can either construct the procedure by filling out the `properties` block or you can specify the create procedure statement in `raw`. Only one of these two values can be specified.

### Argument reference
The following arguments are supported:

- `database` (Required, String) ID of the database where the procedure should be created.
- `name` (Required, String) Name of the procedure
- `schema` (Required, String) ID of the `azuresql_schema` in which the procedure should be created.
- `raw` (Optional, String) SQL query used to create the procedure. When `raw` is specified, `properties` can not be specified.
- `properties` (Optional, `<properties>`) A properties block `properties` as defined below.  When `properties` is specified, `raw` can not be specified.

---
A `properties` block supports the following:

- `arguments` (Optional, List<`argument`>) List of `argument` blocks specifying the input arguments of the procedure. The `argument` block is defined below.
- `executor` (Optional, String) Specify the [execution context](https://learn.microsoft.com/en-us/sql/t-sql/statements/execute-as-clause-transact-sql) for the procedure. Allowed values are `"caller"`, `"self"`, `"owner"` or the name of an existing database user. Default value `"caller"`. Only `executor = "caller"` is supported in Synapse or when defining table valued procedures. 
- `definition` (Required, String) Query executed by the procedure.

---
An `argument` block supports the following:

- `name` (Required, String) name of the variable. This variable can be referenced in the procedure definition as `@<name>`.
- `type` (Required, String) type of the variable.

### Attributes Reference
In addition to the arguments listed above, the following read only attributes are exported:

- `id` (String) azuresql ID of the procedure resource.
- `object_id` (String) ID of the procedure object in the database

## ID structure

The ID is formed as `<database>`/procedure/`<object_id>`, where
* `<database>` is the ID of the `azuresql_database` resource.
* `<object_id>` is the id of the procedure in the database. It can be found by running `select object_id('<procedure schema>.<procedure name>')`.

## Import

You can import a procedure using 

```shell
terraform import azuresql_procedure.<resource name> <id>
```