---
# generated by https://github.com/hashicorp/terraform-plugin-docs
page_title: "azuresql_function Resource - terraform-provider-azuresql"
subcategory: ""
description: |-
  Manage database user defined functions.
---

# azuresql_function (Resource)

Manage database user defined functions.

**Supported**: `SQL Database`, `Synapse serverless database` 

**Not supported**: `Synapse dedicated database`


## Example Usage

```terraform
provider "azuresql" {
}

data "azuresql_sqlserver" "server" {
  server = "mysqlserver"
}

data "azuresql_database" "database" {
  server = data.azuresql_sqlserver.server.id
  name   = "mydatabase"
}


data "azuresql_schema" "dbo" {
    database 	= data.azuresql_database.database.id
    name 		  = "dbo"
}

# Define a function via the properties argument
resource "azuresql_function" "sum_props" {
  database = data.azuresql_database.database.id
  name     = "sum"
  schema   = data.azuresql_schema.dbo.id
  properties = {
    arguments = [
      {
        name = "a"
        type = "int"
      },
      {
        name = "b"
        type = "int"
      }
    ]
    executor      = "self"
    return_type   = "int"
    schemabinding = true
    definition    = "@a + @b"
  }
}

# Define the same function via the raw argument
resource "azuresql_function" "sum_raw" {
    database 	  = data.azuresql_database.database.id
    name        = "sum2"
    schema		  = data.azuresql_schema.dbo.id
    raw         = <<-EOT
        create function dbo.sum2(@a int, @b int)
        returns int
        with execute as self, schemabinding
        as 
        BEGIN
          return @a + @b
        END
    EOT
}
```

~> Hint: Terraform will not automatically detect all dependencies on other azuresql resources specified as text in the `raw` or `defintion` parameter. This can be resolved by adding a `replace_triggered_by` lifecycle rule to the `azuresql_function` resource.

<!-- schema generated by tfplugindocs -->
## Schema

-> `azuresql_function` offers two constructors. You can either construct the function by filling out the `properties` block or you can specify the create function statement in `raw`. Only one of these two values can be specified.

### Argument reference
The following arguments are supported:

- `database` (Required, string) ID of the database where the function should be created.
- `name` (Required, string) Name of the function
- `schema` (Required, string) ID of the `azuresql_schema` in which the function should be created.
- `raw` (Optional, string) SQL query used to create the function. When `raw` is specified, `properties` can not be specified.
- `properties` (Optional, `<properties>`) A properties block `properties` as defined below.  When `properties` is specified, `raw` can not be specified.

---
A `properties` block supports the following:

- `arguments` (Optional, list<`argument`>) List of `argument` blocks specifying the input arguments of the function. The `argument` block is defined below.
- `return_type` (Required, string) Return type of the function.
- `executor` (Optional, string) Specify the [execution context](https://learn.microsoft.com/en-us/sql/t-sql/statements/execute-as-clause-transact-sql) for the function. Allowed values are `"caller"`, `"self"`, `"owner"` or the name of an existing database user. Default value `"caller"`. Only `executor = "caller"` is supported in Synapse or when defining table valued functions. 
- `schemabinding` (Optional, bool) When set to `true`, the database determines the referenced resources in the function and prevents their modification in any way that would break the function. Default is `true`.
- `definition` (Required, string) Query executed by the function.

---
An `argument` block supports the following:

- `name` (Required, string) name of the variable. This variable can be referenced in the function definition as `@<name>`.
- `type` (Required, string) type of the variable.

### Attributes Reference
In addition to the arguments listed above, the following read only attributes are exported:

- `id` azuresql ID of the function resource.
- `object_id` ID of the function object in the database

## ID structure

The ID is formed as `<database>`/function/`<object_id>`, where
* `<database>` is the ID of the `azuresql_database` resource.
* `<object_id>` is the id of the function in the database. It can be found by running `select object_id('<function schema>.<function name>')`.

## Import

You can import a function using 

```shell
terraform import azuresql_function.<resource name> <id>
```